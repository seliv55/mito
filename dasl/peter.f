C     THIS COMPUTER PROGRAM, WRITTEN IN FORTRAN, MODELS O2 TRANSPORT FROM
C     AIR TO MITOCHONDRIA, AND THEN COUPLES THIS TO O2 UTLIZATION BY THE 
C     MITOCHONDRIA.
C
C     THE PROGRAM ANSWERS THE QUESTION: GIVEN THE TRANPSORT CAPACITY OF
C     THE LUNGS, HEART, BLOOD AND MUSCLES (CHARACTERIZED BY NUMERICAL 
C     VALUES FOR THE MAJOR TRANSPORT VARIABLES), HOW MUCH O2 CAN BE 
C     SUPPLIED TO THE TISSUES, AND WHAT ARE THE PO2 VALUES AT EACH STEP?
C     
C     O2 TRANSPORT IS ACCOMPLISHED BY 4 ORGANS/TISSUES - LUNGS, HEART,
C     BLOOD AND MUSCLES. TRANSPORT THROUGH THIS SYSTEM INVOLVES FOUR 
C     SEQUENTIAL PROCESSES OR STEPS - 
C
C     1. VENTILATION TO BRING O2 FROM AIR TO THE ALVEOLAR GAS
C
C     VO2  =  k x VA x [PIO2 - PAO2]
C
C     2. DIFFUSION OF O2 FROM ALVEOLAR GAS INTO CAPILLARY BLOOD
C
C     PaO2 - PvO2  =  (PAO2 - PvO2) x exp(-DLO2/(b x Q)) 
C
C     3. CIRCULATORY TRANSPORT OF O2 FROM LUNGS TO TISSUE MICROVESSELS
C
C     VO2 = Q x [CaO2 - CvO2]
C
C     4. DIFFUSION OF O2 FROM TISSUE MICROVESSELS TO MITOCHONDRIA  
C
C     PvO2 - PmitoO2  =  (PaO2 - PmitoO2) x exp(-DMO2/(b x Q))
C
C
C     MODELING IS BASED ON WELL-ESTABLISHED PRINCIPLES OF MASS CONSERVATION
C     FOR O2 AT EVERY STEP, USING CORRESPONDINGLY WELL-ESTABLISHED
C     TRANSPORT EQUATIONS. 
C     THERE IS ONE EQUATION FOR EACH OF THE ABOVE 4 STEPS.
C
C     A FIFTH EQUATION BRINGS IN MITOCHONDRIAL METABOLISM. IT EXPRESSES THE
C     RATE OF MITOCHONDRIAL O2 CONSUMPTION AS AN EXPONENTIAL, TWO PARAMETER
C     FUNCTION OF MITOCHONDRIAL PO2. THE TWO PARAMETERS ARE THE P50 AND VO2MAX
C     OF THE EXPONENTIAL RELATIONSHIP  
C
C     5.  VO2  =  VO2max x [1  -  exp(-0.693 x PmitoO2 / P50)] 
C
C     IN THE ABOVE SYSTEM, THE INDEPENDENT VARIABLES ARE:
C
C     PIO2 (Inspired PO2, mm Hg)
C     VA (alveolar ventilation, L/min)  
C     DLO2 (lung diffusing capacity, ml/min/mm Hg)
C     b (slope of the O2-Hb dissociation curve, ml/ml/mm Hg)
C     Q (cardiac output, l/min)
C     DMO2 (muscle diffusing capacity, ml/min/mm Hg)
C     VO2max is maximal mitochondrial VO2 when O2 is in excess, ml/min
C     P50 is the PO2 at which mitochondrial VO2 is half maximal
C
C     IMPLICIT IN THE ABOVE IS KNOWING THE HbO2 DISSOCIATION CURVE IN A 
C     QUANTITATIVE SENSE. This means that if we know PO2, we know [O2] 
C
C     THE DEPENDENT VARIABLES OR "MODEL OUTPUTS" ARE:
C
C     VO2 (O2 consumed, liters per minute)
C     PAO2 (Alveolar PO2, mm Hg)
C     PaO2 (Arterial PO2, mm Hg). CaO2 is the [O2] at PaO2
C     PvO2 (Venous PO2, mm Hg). CvO2 is the [O2] at PvO2
C     PmitoO2 (Mitochondrial PO2, mm Hg)
C
C     AS SHOULD BE CLEAR, WE HAVE 5 EQUATIONS AND 5 UNKNOWNS. THIS MEANS
C     A UNIQUE SOLUTION CAN BE OBTAINED. THIS PROGRAM FINDS THE SOLUTION FOR 
C     DESIRED SET OF INPUT VARIABLES.
C
C     THERE ARE SOME IMPORTANT ASSUMPTIONS/APPROXIMATIONS:
C
C     1. O2 TRANSPORT IS IN A STEADY STATE - ALL INPUT AND OUTPUT VARIABLES
C        ARE CONSTANT IN TIME
C     2. THE LUNGS ARE CURRENTLY ASSUMED TO BE HOMOGENEOUS
C     3. THE TISSUES ARE LIKEWISE ASSUMED TO BE HOMOGENEOUS
C
C     IMPORTANTLY, THIS VERSION OF THE PROGRAM USES THE ACTUAL O2 AND CO2
C     DISSOCIATION CURVES AND MAKES NO LINEAR APPROXIMATION TO EITHER. THIS
C     ALSO MEANS THAT O2-CO2 INTERACTION IS ACCOUNTED FOR. HOWEVER, THIS
C     MAKES THE SOLUTIONS POSSIBLE ONLY VIA TEDIOUS NUMERICAL ANALYSIS. 
C     CLOSED FORM, ALGEBRAIC SOLUTIONS ARE INFEASIBLE. 
C
C     THE LOGIC IS TO CYCLE THE SYSTEM BETWEEN LUNGS AND TISSUES UNTIL 
C     CONVERGENCE AND STABILITY OF OUTCOME VARIBALES IS ESTABLISHED. IN THE
C     LUNGS, STARTING WITH A TRIAL VENOUS AND ALEVOLAR PO2, THE ARTERIAL PO2
C     IS DETERMINED BY CALCULATING THE ENDCAPILLARY VALUE ACCORDING TO THE
C     DIFFUSION EQUATION (EQN 2). THIS OUTPUT ARTERIAL PO2 THEN BECOMES THE
C     INPUT ARTERIAL PO2 FOR TISSUE O2 DIFFUSION ASSUMING A MITOCHONDRIAL
C     PO2 TO START. THIS YIELDS A VENOUS PO2, AND THEN WE CYCLE BACK TO THE
C     LUNG WITH THIS NEW VENOUS PO2 AND SO ON. 
C
C     ULTIMATELY, STABILITY IS REACHED AND AT THAT POINT, VO2 WILL BE THE
C     SAME NO MATTER WHICH OF THE 5 EQUATIONS IS USED TO COMPUTE IT.  
C
C     THIS VERSION, LUNGTIS3, HAS A BISECTION SEARCH ALGORITHM THAT FINDS
C     THE UNIQUE MITOCHONDRIAL PO2 THAT SATISFIES SIMULTANEOUSLY THE 4-EQN O2
C     TRANSPORT MODEL FROM MOUTH TO MITOCHONDRIA AND THE MITOCHONDRIAL
C     RESPIRATION CURVE (EQN 5).
C
C---------------------------------------------------------------------------
C    
C     TECHNICAL NOTE:
C 
C     BOTH LUNG AND MUSCLE DIFFUSING CAPACITY ARE CONSTANT LUMPED-PARAMETER
C     VARIABLES IN THIS VERSION BECAUSE THAT IS HOW THEY ARE USUALLY MEASURED.
C     IN REALITY, THEY CONTAIN A TRUE DIFFUSION COMPONENT PLUS AN ELEMENT THAT
C     RECOGNIZES THAT CHEMICAL COMBINATION WITH Hb IS NOT INSTANTANEOUS. IN
C     ADDITION, THE ALGORITHM USES AN ARBITRARY VALUE FOR VOLUME OF BLOOD IN
C     THE LUNG/TISSUE CAPILLARIES (CALLED VC). WHILE VC IS TECHNICALLY NEEDED
C     TO COMPUTE TRANSIT TIME, THE FINAL  RESULT DEPENDS ONLY ON DLO2/Q, OR
C     DMO2/Q, AND IS INDEPENDENT OF CHOICE OF VC.
C     IN OTHER WORDS, CHOOSING A LARGE VC GIVES A LONG TRANSIT TIME FOR
C     A GIVEN BLOOD FLOW RATE, BUT THE TIME COURSE IS PRECISELY SLOWED
C     DOWN IN PROPORTION SO THAT ENDCAPILLARY AND MEAN CAPILLARY DATA
C     ARE UNAFFECTED.
C 
      SUBROUTINE deliv()
      COMMON HB,HCRIT,TEMP,APH,BPH,APCO2,BPCO2,P50,O2SOL,VO2,VCO2 
C
C     DEFAULT INPUT VARIABLE VALUES ARE PROVIDED TO EASE DATA ENTRY:
C
C     PVO2 & PVCO2 ARE MIXED VENOUS PO2/PCO2 FOR LUNG DIFFUSION COMPUTATION
C
      PVO2=20.0
      PVCO2=75.0
C
C     PALVO2 & PALVCO ARE ALVEOLAR PO2/PCO2 FOR LUNG DIFFUSION COMPUTATION
C
      PALVO2=100.0
      PALVCO=35.0
C
C     VA IS DEFAULT VENTILATION; PB BAROMETRIC PRESSURE; FIO2 INSPIRED O2
C     FRACTION; PIO2 IS CORRESPONDING INSPIRED PO2; FICO2 IS ASSUMED ZERO; 
C
      VA=85.0
      PB=760.0
      FIO2=0.2093
      PIO2=FIO2*(PB-50.0)
C
C     FAO2 IS DEFAULT ALVEOLAR O2 FRACTION; DITTO FOR CO2
C     REXP IS THE ASSUMED RESP EXCHANGE RATIO OR RQ (=VCO2/VO2) AT VO2max
C     IT SHOULD BE REDUCED AT LOWER EXERCISE INTENSITIES. IT IS HELD CONSTANT.
C
      FAO2=PALVO2/(PB-50.0)
      FACO2=PALVCO/(PB-50.0)
      REXP=1.15
C
C     NOW FOLLOW IN ORDER :
C
C  O2SOL    O2 PHYSICAL SOLUBILITY IN WATER/BLOOD, ML/DL/MM HG
C  HB       HEMOGLOBIN, GM/100 ML
C  HRCIT    HEMATOCRIT (PER CENT), SET AT 3 * HB
C  TEMP     BODY TEMPERATURE, DEGREES CENTIGRADE
C  P50      O2 HEMOGLOBIN DISSOCIATION CURVE P50, TORR
C  APH      PH OF BLOOD EQUILIBRATED WITH PCO2 OF 30 TORR
C  APCO2    CORRESPONDING PCO2 (30 TORR)
C  BPH      PH OF BLOOD EQUILIBRATED WITH PCO2 OF 60 TORR
C  BPCO2    CORRESPONDING PCO2 (60 TORR)   
C  QT       TOTAL TISSUE OR PULMONARY BLOOD FLOW, L/MIN
C  VC       CAPILLARY BLOOD VOLUME, ML
C  DMO2     MEMBRANE DIFFUSING CAPACITY FOR O2, ML/MIN/TORR
C  DT       TIME STEP INTERVAL FOR NUMERICAL INTEGRATION, SECONDS
C
      O2SOL=0.003
      HB=15.0
      HCRIT=3.0*HB
      ITHETA=0
      TEMP=38.0
      P50=26.8
      APH=7.30
      APCO2=30.0
      BPH=7.10
      BPCO2=60.0
      QT=20.0
      VC=5.0*QT
      DLNGO2=60.0
      DMUSO2=80.0
      DT=0.001
C
C     HERE ARE THE CONSTANTS FOR THE STAUB ET AL THETA-O2 RELATIONSHIP:
C SHOULD ONE WANT TO MODEL DIFFUSION INCORPORATING CHEMICAL REACTION RATES
C
C     AS STATED, WE DO NOT DO THIS BECAUSE EXPERIMENTAL DATA ARE FOR TOTAL
C     DLO2 INCORPORATING THIS EFFECT ALREADY
C
C      B1=36.1633
C      B2=-1.5551
C      B3=0.0234
C      B4=-1.15173E-4
C
C     HERE IS THE HALF TIME OF THE CO2 CHEMICAL REACTIONS, SECONDS
C     SAME LOGIC FOR CO2 DIFFUSION AND ITS CHEMICAL REACTIONS IN BLOOD
C
C      THALF=0.15
C
C     THE PROCEDURE BEGINS WITH CHOICES REGARDING OUTPUT OPTIONS:
C
C
  10  CONTINUE
      IPR=1;
C
C     YOU HAVE TO ENTER YOUR CHOSEN NAME FOR THE OUTPUT FILE NOW
C     THE PLOT FILE TABULATES THE POINT BY POINT TIME COURSE OF PO2 AND PCO2
C     CHANGE ALONG THE LUNG AND TISSUE CAPILLARIES. THE USER NAMES THIS FILE 
C     AT THIS POINT. THE FORMAT IS 10 COLUMNS, 8 DIGITS EACH
C     EACH HORIZONTAL ROW GIVES RESULTS AT ONE TIME POINT FOR ALL THE
C     VARIABLES. EACH COLUMN THEREFORE TABULATES ONE VARIABLE IN TIME
C     THE TEN VARIABLES, FROM LEFT TO RIGHT IN THE TABLE ARE:
C
C  1   TIME,  SECONDS
C  2   PLASMA PO2,  TORR
C  3   RED CELL PO2,  TORR
C  4   BLOOD O2 CONTEN,  ML/100 ML
C  5   PLASMA PCO2,  TORR
C  6   RED CELL PCO2,  TORR
C  7   BLOOD CO2 CONTENT,  ML/100 ML
C  8   INSTANTANEOUS O2 DIFFUSING CAPACITY,  ML/MIN/TORR
C  9   CORRESPONDING CO2 DIFFUSING CAPACITY,  ML/MIN/TORR
C  10  BLOOD O2 SATURATION,  %
C
      OPEN(UNIT=9,FILE='r2')
      WRITE(9,77)
  77  FORMAT('mm hg: venous o2-co2   alv-tis       arterial')
C
C*************************************************************
C     KEYBOARD ENTRY OF THE TWO MITOCHONDRIAL CURVE PARAMETERS -
C     MAXIMAL VO2 AND P50 OF THE EXPONENTIAL RESPIRATION CURVE 
C
      VO2MAX=3000.
      P50MIT=5.
C
C     NOW FOLLOWS THE BISECTION SEARCH FOR THE ACTUAL MITOCHNDIRAL PO2.
C     THIS IS IN FACT SOLVING ONE OF THE 5 MAIN EQUATIONS
C     THE 3000 LOOP ACCOMPLISHES THIS
C
      PTMIN=0.0
      PTMAX=40.0
 3000 PTISO2=(PTMIN+PTMAX)/2.0
C
C     PTISO2 IS THE TRIAL MITOCHONDRIAL PO2, AND BECOMES THE CORRECT VALUE 
C     WHEN CONVERGENCE HAS BEEN REACHED
C*************************************************************     
      RKMITO=0.693/P50MIT
      REXP=RKMITO*PTISO2
      VO2ACT=VO2MAX*(1.0 - EXP(-REXP))
  90  CONTINUE
      FAO2=PALVO2/(PB-50.0)
      FACO2=PALVCO/(PB-50.0)
      REXP=1.15
      PIO2=(PB-50.0)*FIO2
C********************
      NOUT=9
C********************
      DO 210 JJ=1,NOUT
      FAO2=PALVO2/(PB-50.0)
      FACO2=PALVCO/(PB-50.0)
C     CYCLE BETWEEN THE LUNG AND TISSUE INTEGRATION NOW
C     THIS IS A BOHR INTEGRATION FOR BOTH LUNG AND TISSUES
C     IT IS A SIMPLE ALGORITHM WITHOUT RUNGE-KUTTA,
C     JUST A GRADIENT METHOD WHERE ACCURACY DEPENDS
C     ON SUFFICIENTLY SMALL TIME STEPS
C*******************
      IF(JJ.GT.1) GO TO 170
      NCYC=3
C*******************
 170  CONTINUE
C
C****************************************************************
C
C     THE LOOP BELOW, 190, IS THE MAIN LOOP CYCLING BACK AND FORTH BETWEEN 
C     LUNGS AND TISSUES
C
C****************************************************************
C
      WRITE(9,77)
      DO 190 I=1,NCYC
      ILT=0
C     ILT=0 SIGNALS LUNG BOHR INTEGRAL*********************************
C PVO2-venous po2, PVCO2-venous pco2, PALVO2-alveolar po2, PALVCO- alveolar pco2, PO2- arterial po2, PCO2- arterial pco2
      DMO2=DLNGO2
      CALL LT(JJ,I,NCYC,ILT,IPR,IPL,PVO2,PVCO2,PALVO2,PALVCO,PO2,PCO2,
     1QT,DMO2,DT)
      WRITE(9,79)PVO2,PVCO2,PALVO2,PALVCO,PO2,PCO2
  79  FORMAT('L: ',6F8.2)
C      READ(*,*)GARB
      PARTO2=PO2
      PARTCO=PCO2
C      PTISO2=0.0
C***************************
      RR=VCO2/VO2
      FACTOR=1.15/RR
      PTISCO=PVCO2*FACTOR
C****************************************************************
C      WRITE(*,180)I,ILT,VO2,VCO2,PALVO2,PALVCO,PARTO2,PARTCO,PVO2,PVCO2,
C     1PTISO2,PTISCO
C*****************************************************************
C      WRITE(*,79)
C      READ(*,*)GARB
      ILT=1
C
C     ILT=1 SIGNALS TISSUE INTEGRAL************************************
C
C PVO2-venous po2, PVCO2-venous pco2, PALVO2-alveolar po2, PALVCO- alveolar pco2, PO2- arterial po2, PCO2- arterial pco2
      DMO2=DMUSO2
      CALL LT(JJ,I,NCYC,ILT,IPR,IPL,PARTO2,PARTCO,PTISO2,PTISCO,PO2,
     1PCO2,QT,DMO2,DT)
      WRITE(9,80)PO2,PCO2,PTISO2,PTISCO,PARTO2,PARTCO
  80  FORMAT('T: ',6F8.2)
C      WRITE(*,79)
C      READ(*,*)GARB
      PVO2=PO2
      PVCO2=PCO2
C*******************************************************************
C      WRITE(*,180)I,ILT,VO2,VCO2,PALVO2,PALVCO,PARTO2,PARTCO,PVO2,PVCO2,
C     1PTISO2,PTISCO
C*********************************************************************
C      WRITE(*,79)
C      READ(*,*)GARB
 190  CONTINUE
C******************************************************************
C
C     END OF THE MAIN 190 DO LOOP
C
C******************************************************************
      REXP=VCO2/VO2
      FUDGE=1.0 - FIO2*(1.0-REXP)
      VO2EXP=1000.0*VA*(FIO2-FAO2)/FUDGE
      VCO2EX=1000.0*VA*FACO2
      O2TRY=(PB-50.0)*(FIO2 - VO2*FUDGE/(1000.0*VA))
      CO2TRY=(PB-50.0)*VCO2/(1000.0*VA)
C******************************************************************
C      WRITE(*,200)JJ,VO2EXP,VO2,PALVO2,O2TRY,VCO2EX,VCO2,PALVCO,CO2TRY,
C     1REXP
C*********************************************************************
      PALVO2=(O2TRY+PALVO2)/2.0
      PALVCO=(CO2TRY+PALVCO)/2.0
 210  CONTINUE   
C******************************************************
C
C     END OF THE MITOCHONDRIAL PO2 BISECTION SEARCH
C 
C******************************************************
      CLOSE(9)
      WRITE(*,220)
 220  FORMAT(' WANT TO GO BACK AND DO IT AGAIN ? (YES=1, NO=0)')
      READ(*,*)IMOR
      CLOSE(8)
      CLOSE(3)
 240  CONTINUE
      END
C**********************************************************************
C     THIS ENDS THE PROCEDURE, FOLLOWING ARE ALL THE SUPPORTING SUBROUTINES
C**********************************************************************
C
      SUBROUTINE LT(JJ,ICYC,NCYC,ILT,IPR,IPL,PINO2,PINCO2,ALTSO2,ALTSCO,
     1PO2,PCO2,QT,DMO2,DT)
      DIMENSION TIME(1000),O2PLAS(1000),O2RBC(1000),O2CONC(1000),
     1CO2PLS(1000),CO2RBC(1000),CO2CNT(1000),DIFO2(1000),
     2DIFCO2(1000),O2SAT(1000),YY(1000)
      COMMON HB,HCRIT,TEMP,APH,BPH,APCO2,BPCO2,P50,O2SOL,VO2,VCO2 
C     NOW FOLLOWS DATA ENTRY SECTION TOREPLACE DEFAULTS IF DESIRED
C     VARIABLES ARE ENTERED FORMAT FREE (DECIMALS OPTIONAL, COMMAS
C     TO SEPARATE)
C     THE INPUT DATA ARE ALSO PRINTED OUT NOW (IF HARDCOPY DESIRED)
 998  CONTINUE
      VC=5.0*QT
      QMS=QT*1000.0/60.0
C     QMS IS TOTAL BLOOD FLOW, ML/SEC
C      TT=VC*60.0/QT/1000.0
      TT=0.3
C     TT IS TRANSIT TIME (SECONDS) COMPUTED AS CAPILLARY VOLUME/BLOOFLOW
C     ITS VALUE IS ONLY AS CORRECT AS THE ASSUMED VALUE OF VC FROM WHICH
C     IT IS DERIVED.HOWEVER, THE NUMERICAL RESULTS ARE TOTALLY INDEPENDENT
C     OF CHOICE OF VC AND HENCE TT - DEPENDING ON DO2/Q ONLY.
C
      DMO2S=DMO2/60.0
      DMCO2S=20.0*DMO2S
C
C     DMO2S= MEMBRANE O2 DIFF CAP, ML/SEC/TORR
C     DMCO2S=MEMBRANE CO2 DIFF CAP, ML/SEC/TORR, ASSUMED 20 x THAT FOR O2
C     BASED ON DIFFERENCES IN SOLUBILITY AND MOLECULAR WEIGHT
C
C     ALL QUANTITIES ARE NOW IN SECONDS, TORR & ML
C
      CALL BLOOD(PINO2,PINCO2,CINO2,CINCO2,SATRN)
      PO2=PINO2
      PLASPO=PINO2
      PCO2=PINCO2
      PLASPC=PINCO2
      O2C=CINO2
      O2DEL=10.0*QT*O2C
      CO2C=CINCO2
      PBARO2=PO2
      PBARCO=PCO2
      T=0.0
C**********
C     HERE IS THE DIFFUSION LOOP ITSELF (DO 350 IS OUTER LOOP, 340 IS INNER)
C**********
      PRK=0.01/DT
      KPRINT=PRK
      IF(KPRINT.LE.0) KPRINT=1
      K=0
      DSUM=0.0
      DO 350 J=1,1000
      DO 340 I=1,KPRINT
      K=K+1
C     NOW FOLLOWS DO2 COMPUTATION A LA ROUGHTON & FORSTER, 1957
C
C      IF(SATRN.LE.76.0) THETA=2.8
C      IF(SATRN.GT.76.0.AND.SATRN.LT.94.0)
C     1THETA=B1+B2*SATRN+B3*SATRN**2+B4*SATRN**3
C      IF(SATRN.GE.94.0) THETA=24.0-0.24*SATRN
C      THETA=THETA/60.0
C****************
C     THIS LINE REDUCES THETA IN PROPORTION TO HB
C     IF YOU WISH (IE., IF ITHETA IS SET TO 1)
C
C      IF(ITHETA.EQ.1) THETA=THETA*HB/15.0
C****************
C
C     THIS PUTS THETA IN ML/SEC/ML/TORR
C
C      DINV=1.0/DMO2S + 1.0/(VC*THETA)
      IF(K.GT.1) DSUM=DSUM+DMO2
C     NOW FOLLOWS DCO2 COMPUTATION USING HALFTIME
C     OF 0.15 SECONDS AND SAME CONCEPT AS FOR O2:
C     1/D  =  1/DMCO2  +  1/(THETA * VC)
C     THETA IS COMPUTED FROM THE HALFTIME AS FOLLOWS:
C 
C      AA=41.58*CO2C*VC
C      BB=THALF*PCO2*100.0
C      AB=AA/BB
C      AB=AB/60.0
C      DINV=1.0/DMCO2S + 1.0/AB
      RK=K
C     THE ABOVE PUTS DO2 AND DCO2 AS PRINTED IN ML/MIN/TORR
C
      TIME(K)=T
      O2PLAS(K)=PLASPO
      O2RBC(K)=PO2
      O2CONC(K)=O2C
      CO2PLS(K)=PLASPC
      CO2RBC(K)=PCO2
      CO2CNT(K)=CO2C
      DIFO2(K)=DMO2
      DIFCO2(K)=DMCO2
      O2SAT(K)=SATRN
      IF(T.GT.0.9999*TT) GO TO 360
C     T IS TOTAL ELAPSED TIME IN THE CAPILLARY; DT THE INTEGRAL ELEMENT
C     OF TIME USED IN THE FORWARD INTEGRATION
      T=T+DT
C     NOW FOLLOWS THE ACTUAL INTEGRATION PROCEDURE
      O2C=O2C+DMO2S*(ALTSO2-PO2)*DT*100.0/VC
      CO2C=CO2C+DMCO2S*(ALTSCO-PCO2)*DT*100.0/VC
      CALL FNDCON(PO2,PCO2,O2C,CO2C,ILT,SATRN)
C     FNDCON IS THE ROUTINE THAT GETS YOU A PO2 FROM AND [O2]
      PLASPO=ALTSO2-(ALTSO2-PO2)
      PLASPC=ALTSCO+(PCO2-ALTSCO)
      PBARO2=(PBARO2*RK+PO2)/(RK+1.0)
      PBARCO=(PBARCO*RK + PCO2)/(RK+1.0)
 340  CONTINUE     
 350  CONTINUE
 360  CONTINUE
C     THIS ENDS THE DIFFUSION INTEGRATION LOOP
C     OVERALL OUPUT DATA ARE NOW COMPUTED AND PRINTED
      VO2=ABS(10.0*QT*(CINO2-O2C))
      IF(ILT.EQ.0) O2DEL=O2DEL + VO2
      O2EXT=100.0*VO2/O2DEL
      VCO2=ABS(10.0*QT*(CINCO2-CO2C))
      R=VCO2/VO2
      DIFFPO=ABS(ALTSO2-PO2)
      DIFFPC=ABS(ALTSCO-PCO2)
      IF(ICYC.NE.NCYC) RETURN
      RETURN
      END

      SUBROUTINE BLOOD(PO2,PCO2,O2C,CO2C,SATRN)
C     COMPUTES O2 & CO2 CONTENTS FROM PARTIAL PRESSURES, CALLING THE 
C     DISSOCIATION CURVE ROUTINES THAT FOLLOW
      COMMON HB,HCRIT,TEMP,APH,BPH,APCO2,BPCO2,P50,O2SOL
      PH1=PH(PCO2,0.0)
      Y=0.003*HB*(1.0-SATURA(PO2,PCO2,PH1)/100.0)
      PH2=PH(PCO2,Y)
      SATRN=SATURA(PO2,PCO2,PH2)
      O2C=0.0139*HB*SATRN + O2SOL*PO2
      CO2C=CO2CON(PCO2,PH2,SATRN)
      RETURN
      END

      FUNCTION PH(PPCO2,Y)
C     COMPUTES PH FROM BUFFER DATA AND PCO2
      COMMON HB,HCRIT,TEMP,APH,BPH,APCO2,BPCO2,P50,O2SOL
      IF(PPCO2.LT.0.001) PPCO2=0.001
      IF(APH-1.)10,10,20
  10  PH=7.590+Y-.2741*ALOG(PPCO2/20.)
      GO TO 30
  20  PH=BPH+Y+(APH-BPH)*ALOG(PPCO2/BPCO2)/ALOG(APCO2/BPCO2)
  30  RETURN
      END
      
      FUNCTION CO2CON(PCO2,PHE,SATN)
C     KELMAN'S CO2 DISSOCIATION CURVE
      COMMON HB,HCRIT,TEMP,APH,BPH,APCO2,BPCO2,P50,O2SOL
      P=7.4-PHE
      PK=6.086+.042*P+(38.-TEMP)*(.00472+.00139*P)
      SOL=.0307+.00057*(37.-TEMP)+.00002*(37.-TEMP)*(37.-TEMP)
      DOX=.59+.2913*P-.0844*P*P
      DR=.664+.2275*P-.0938*P*P
      DDD = DOX + (DR - DOX)*(1.-SATN/100.)
      CP=SOL*PCO2*(1.+10.**(PHE-PK))
      CCC = DDD*CP
      CO2CON = (HCRIT*CCC*.01 + (1. - HCRIT*.01)*CP)*2.22
      RETURN
      END
      
      FUNCTION SATURA(PPO2,PPCO2,PHE)
C     KELMAN'S O2 DISSOCIATION CURVE
      COMMON HB,HCRIT,TEMP,APH,BPH,APCO2,BPCO2,P50,O2SOL
      A1=-8.532229E3
      A2=2.121401E3
      A3=-6.707399E1
      A4=9.359609E5
      A5=-3.134626E4
      A6=2.396167E3
      A7=-6.710441E1
      B=.43429*ALOG(40.)-.43429*ALOG(PPCO2)
      X=PPO2*10.**(.024*(37.-TEMP)+.4*(PHE-7.4)+.06*B)
      X=X*26.8/P50
      IF(X-10.)10,20,20
  10  SAT=0.003683*X+0.000584*X*X
      GO TO 30
  20  SAV2 = X*(X*(X*(X+A3)+A2)+A1)
      SAV3 = X*(X*(X*(X+A7)+A6)+A5)+A4
      SAT = SAV2/SAV3
  30  SATURA=100.*SAT
      RETURN
      END
      
      SUBROUTINE FNDCON(PO2,PCO2,OXO2,COCON,ILT,SATRN)
C     THIS SUBROUTINE FINDS THE PO2 AND PCO2 THAT CORRESPOND TO A GIVEN PAIR OF
C     CONTENTS. THIS IS ACHIEVED BY A TWO DIMENSIONAL  NEWTON-RAPHSON ITERATIVE
C     PROCEDURE ANALOGOUS TO THAT USED TO MATCH V/Q RATIOS.
C     ACCURACY OF THE MATCH IS FORCED TO WITHIN 0.001 ML/100ML BLOOD
      DIMENSION X(4),Y(4),F(4),G(4),U(4)
      DOUBLE PRECISION U,F,G,DET1,DET2
      COMMON HB,HCRIT,TEMP,APH,BPH,APCO2,BPCO2,P50,O2SOL
      TOL=0.001
      DO 10 KK=1,4
      F(KK)=0.0
      G(KK)=0.0
      U(KK)=0.0
  10  CONTINUE
      ITER=0
      NNN=3
      X(1)=PO2
      Y(1)=PCO2
      X(2)=PO2 + 5.0*(-1.0)**ILT
      Y(2)=PCO2
      X(3)=PO2
      Y(3)=PCO2 - 5.0*(-1.0)**ILT
  20  DO 50 N=1,NNN
      PH1 = PH(Y(N),0.0)
      Y1 = .003*HB*(1. - SATURA(X(N),Y(N),PH1)/100.)
      PH2 = PH(Y(N),Y1)
      SATRN = SATURA(X(N),Y(N),PH2)
      F(N) = .0139*HB*SATRN + O2SOL*X(N) - OXO2
      G(N) = CO2CON(Y(N),PH2,SATRN) - COCON
      IF(ABS(F(N))-TOL)30,30,50
  30  IF(ABS(G(N))-TOL)40,40,50
  40  PO2 = X(N)
      PCO2 = Y(N)
      GO TO 170
  50  CONTINUE
      DO 60 N=1,3
      U(N)=1.
  60  CONTINUE
      CALL DETERM(U,F,G,DET1)
      DO 70 N=1,3
      U(N)=X(N)
  70  CONTINUE
      NFLAG=0
  80  CALL DETERM(U,F,G,DET2)
      IF(NFLAG-1)90,100,100
  90  IF(DET1.EQ.0.0) WRITE(*,180)
      X(4)=DET2/DET1
      GO TO 110
 100  IF(DET1.EQ.0.0) WRITE(*,180)
      Y(4)=DET2/DET1
 110  IF(NFLAG-1)120,140,140
 120  DO 130 N=1,3
      U(N)=Y(N)
 130  CONTINUE
      NFLAG=1
      GO TO 80
 140  DO 150 N=1,2
      J=4-N
      X(J)=X(J-1)
      Y(J)=Y(J-1)
      F(J)=F(J-1)
      G(J)=G(J-1)
 150  CONTINUE
      X(1)=X(4)
      Y(1)=Y(4)
      NNN=1
      ITER=ITER+1
      IF(ITER-30)160,170,170
 160  GO TO 20
 170  CONTINUE
 180  FORMAT(' DET1 IS ZERO IN FNDCON')
      RETURN
      END
      SUBROUTINE DETERM(U,F,G,DET)
C
C     THIS SUBROUTINE IS USED BY FNDCON TO SOLVE THE NUMERCIAL
C     EQUATIONS NECESSARY IN COMPUTING PARTIAL PRESSURES FROM CONTENTS
C
      DIMENSION U(4),F(4),G(4),W(4)
      DOUBLE PRECISION U,F,G,W,D,DET
      DO 10 KK=1,4
  10  W(KK)=0.0
      I=1
      J=2
      K=3
  20  W(I)=U(I)*(F(J)*G(K)-F(K)*G(J))
      IF(I-3)30,60,60
  30  IF(I-1)40,40,50
  40  I=2
      J=3
      K=1
      GO TO 20
  50  I=3
      J=1
      K=2
      GO TO 20
  60  D=0.0
      DO 70 I=1,3
      D=D+W(I)
  70  CONTINUE
      DET=D
      RETURN
      END
      
