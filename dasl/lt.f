      SUBROUTINE LT(JJ,ICYC,NCYC,ILT,IPR,IPL,PINO2,PINCO2,ALTSO2,ALTSCO,
     1PO2,PCO2,QT,DLNGO2,DMUSO2,DT,IPTISS)
      DIMENSION TIME(1000),O2PLAS(1000),O2RBC(1000),O2CONC(1000),
     1CO2PLS(1000),CO2RBC(1000),CO2CNT(1000),DIFO2(1000),
     2DIFCO2(1000),O2SAT(1000),YY(1000)
      COMMON HB,HCRIT,TEMP,APH,BPH,APCO2,BPCO2,P50,O2SOL,VO2,VCO2 
C
C     NOW FOLLOWS DATA ENTRY SECTION TOREPLACE DEFAULTS IF DESIRED
C     VARIABLES ARE ENTERED FORMAT FREE (DECIMALS OPTIONAL, COMMAS
C     TO SEPARATE)
C
C     THE INPUT DATA ARE ALSO PRINTED OUT NOW (IF HARDCOPY DESIRED)
C
 999  CONTINUE
C
 180  CONTINUE
      IF(JJ.GT.1) GO TO 998
      IF(ILT.GT.0.OR.ICYC.GT.1) GO TO 998
 240  CONTINUE
      IF(IPR.EQ.1)
     1WRITE(8,250)HB,HCRIT,TEMP,P50,APH,APCO2,BPH,BPCO2,O2SOL
 250  FORMAT('  HB =',F5.1,11X,'HCRIT =',F6.1,10X,
     1'TEMP =',F6.1,5X,'AND P50 =',F6.1,/,
     2' APH =',F6.2,5X,' AND APCO2 =',F6.1,/,
     3' BPH =',F6.2,5X,' AND BPCO2 =',F6.1/,
     4' O2 SOL =',F8.4/)
 290  CONTINUE
 998  CONTINUE
      IF(ILT.EQ.0) DMO2=DLNGO2
      IF(ILT.EQ.1) DMO2=DMUSO2
      VC=5.0*QT
      IF(ICYC.EQ.NCYC.AND.IPR.EQ.1)
     1WRITE(8,300)QT,VC,DMO2,DT
 300  FORMAT(' TOTAL BLOOD FLOW =',F7.3,'  L/MIN',/,
     1' CAPILLARY BLOOD VOLUME =',F7.3,'   ML',/,
     2' DO2 =',F7.3,'   ML/MIN/TORR',/,
     3' TIME STEP FOR INTEGRAL =',F9.5,'  SECONDS'/)
      QMS=QT*1000.0/60.0
C
C     QMS IS TOTAL BLOOD FLOW, ML/SEC
C
      TT=VC/QMS
C
C     TT IS TRANSIT TIME (SECONDS) COMPUTED AS CAPILLARY VOLUME/BLOOFLOW
C     ITS VALUE IS ONLY AS CORRECT AS THE ASSUMED VALUE OF VC FROM WHICH
C     IT IS DERIVED.HOWEVER, THE NUMERICAL RESULTS ARE TOTALLY INDEPENDENT
C     OF CHOICE OF VC AND HENCE TT - DEPENDING ON DO2/Q ONLY.
C
      WRITE(*,310)TT
      IF(IPR.EQ.1)
     1WRITE(8,310)TT
 310  FORMAT(' TRANSIT TIME =',F7.2,'    SECONDS'//)
      DMO2S=DMO2/60.0
      DMCO2S=20.0*DMO2S
C
C     DMO2S= MEMBRANE O2 DIFF CAP, ML/SEC/TORR
C     DMCO2S=MEMBRANE CO2 DIFF CAP, ML/SEC/TORR, ASSUMED 20 x THAT FOR O2
C     BASED ON DIFFERENCES IN SOLUBILITY AND MOLECULAR WEIGHT
C
C     ALL QUANTITIES ARE NOW IN SECONDS, TORR & ML
C
      TNT=TT/DT
      NT=TNT
      CALL BLOOD(PINO2,PINCO2,CINO2,CINCO2,SATRN)
      SATOR=SATRN
      PO2=PINO2
      PLASPO=PINO2
      PCO2=PINCO2
      PLASPC=PINCO2
      O2C=CINO2
      O2DEL=10.0*QT*O2C
      CO2C=CINCO2
      PBARO2=PO2
      PBARCO=PCO2
      T=0.0
      WRITE(*,320)
      IF(ICYC.EQ.NCYC.AND.IPR.EQ.1)WRITE(8,320)
 320  FORMAT(8X,'N',3X,'SECS',2X,'PLASMA',2X,'RBC',4X,'O2CON',2X,
     1'PLASMA',2X,'RBC',3X,'CO2CON',3X,'DO2',4X,'DCO2',1X,'O2SAT')
      WRITE(*,330)
      IF(ICYC.EQ.NCYC.AND.IPR.EQ.1)WRITE(8,330)
 330  FORMAT(19X'PO2'4X'PO2'12X'PCO2'3X'PCO2'/)
C**********
C     HERE IS THE DIFFUSION LOOP ITSELF (DO 350 IS OUTER LOOP, 340 IS INNER)
C**********
      PRK=0.01/DT
      KPRINT=PRK
      IF(KPRINT.LE.0) KPRINT=1
      K=0
      DSUM=0.0
      DO 350 J=1,1000
      WRITE(*,380)J,K,T,PLASPO,PO2,O2C,PLASPC,PCO2,CO2C,DO,DCO,SATRN
      IF(ICYC.EQ.NCYC.AND.IPR.EQ.1)
     1WRITE(8,380)J,K,T,PLASPO,PO2,O2C,PLASPC,PCO2,CO2C,DO,DCO,SATRN
      IF(ICYC.EQ.NCYC.AND.IPL.EQ.1)
     1WRITE(3,370)T,PLASPO,PO2,O2C,PLASPC,PCO2,CO2C,DO,DCO,SATRN
      DO 340 I=1,KPRINT
      K=K+1
C     
C     NOW FOLLOWS DO2 COMPUTATION A LA ROUGHTON & FORSTER, 1957
C
C      IF(SATRN.LE.76.0) THETA=2.8
C      IF(SATRN.GT.76.0.AND.SATRN.LT.94.0)
C     1THETA=B1+B2*SATRN+B3*SATRN**2+B4*SATRN**3
C      IF(SATRN.GE.94.0) THETA=24.0-0.24*SATRN
C      THETA=THETA/60.0
C****************
C     THIS LINE REDUCES THETA IN PROPORTION TO HB
C     IF YOU WISH (IE., IF ITHETA IS SET TO 1)
C
C      IF(ITHETA.EQ.1) THETA=THETA*HB/15.0
C****************
C
C     THIS PUTS THETA IN ML/SEC/ML/TORR
C
C      DINV=1.0/DMO2S + 1.0/(VC*THETA)
      DINV=1.0/DMO2S
      DO2=1.0/DINV
      IF(K.GT.1) DSUM=DSUM+DO2*60.0
C
C     NOW FOLLOWS DCO2 COMPUTATION USING HALFTIME
C     OF 0.15 SECONDS AND SAME CONCEPT AS FOR O2:
C     1/D  =  1/DMCO2  +  1/(THETA * VC)
C     THETA IS COMPUTED FROM THE HALFTIME AS FOLLOWS:
C 
C      AA=41.58*CO2C*VC
C      BB=THALF*PCO2*100.0
C      AB=AA/BB
C      AB=AB/60.0
C      DINV=1.0/DMCO2S + 1.0/AB
      DINV=1.0/DMCO2S
      DCO2=1.0/DINV
      RK=K
      DO=DO2*60.0
      DCO=DCO2*60.0
C
C     THE ABOVE PUTS DO2 AND DCO2 AS PRINTED IN ML/MIN/TORR
C
      TIME(K)=T
      O2PLAS(K)=PLASPO
      O2RBC(K)=PO2
      O2CONC(K)=O2C
      CO2PLS(K)=PLASPC
      CO2RBC(K)=PCO2
      CO2CNT(K)=CO2C
      DIFO2(K)=DO
      DIFCO2(K)=DCO
      O2SAT(K)=SATRN
      IF(T.GT.0.9999*TT) GO TO 360
C
C     T IS TOTAL ELAPSED TIME IN THE CAPILLARY; DT THE INTEGRAL ELEMENT
C     OF TIME USED IN THE FORWARD INTEGRATION
C
      T=T+DT
C
C     NOW FOLLOWS THE ACTUAL INTEGRATION PROCEDURE
C
      O2C=O2C+DO2*(ALTSO2-PO2)*DT*100.0/VC
      CO2C=CO2C+DCO2*(ALTSCO-PCO2)*DT*100.0/VC
      CALL FNDCON(PO2,PCO2,O2C,CO2C,ILT,SATRN)
C
C     FNDCON IS THE ROUTINE THAT GETS YOU A PO2 FROM AND [O2]
C
      PLASPO=ALTSO2-(ALTSO2-PO2)*DO2/DMO2S
      PLASPC=ALTSCO+(PCO2-ALTSCO)*DCO2/DMCO2S
      PBARO2=(PBARO2*RK+PO2)/(RK+1.0)
      PBARCO=(PBARCO*RK + PCO2)/(RK+1.0)
 340  CONTINUE     
 350  CONTINUE
 360  CONTINUE
C
C     THIS ENDS THE DIFFUSION INTEGRATION LOOP
C
 370  FORMAT(F8.3,9F8.2)
 380  FORMAT(1X,I3,I6,F6.3,2F7.1,F8.2,2F7.1,F8.2,2F7.2,F6.1)
C
C     OVERALL OUPUT DATA ARE NOW COMPUTED AND PRINTED
C
      DO2BAR=DSUM/FLOAT(K-1)
      VO2=ABS(10.0*QT*(CINO2-O2C))
      IF(ILT.EQ.0) O2DEL=O2DEL + VO2
      O2EXT=100.0*VO2/O2DEL
      VCO2=ABS(10.0*QT*(CINCO2-CO2C))
      R=VCO2/VO2
      DIFFPO=ABS(ALTSO2-PO2)
      DIFFPC=ABS(ALTSCO-PCO2)
      IF(ILT.EQ.0)
     1WRITE(*,390)PO2,PCO2,DIFFPO,DIFFPC,PBARO2,PBARCO,DO2BAR
      IF(ILT.EQ.1)
     1WRITE(*,400)PO2,PCO2,DIFFPO,DIFFPC,PBARO2,PBARCO,DO2BAR
      WRITE(*,410)VO2,VCO2,R,O2DEL,O2EXT
      IF(ICYC.NE.NCYC) RETURN
      IF(ILT.EQ.0.AND.IPR.EQ.1)
     1WRITE(8,390)PO2,PCO2,DIFFPO,DIFFPC,PBARO2,PBARCO,DO2BAR
 390  FORMAT(//' ENDCAPILLARY PO2   =',F8.1,3X,
     1'  AND  PCO2 =',F6.1,//,
     2' ALVEOLAR-ENDCAPILLARY PO2 DIFF =',F5.1,
     3'; ENDCAPILLARY-ALVEOLAR PCO2 DIFF =',F5.1,//,
     4' MEAN CAPILLARY PO2 =',F8.1,4X,
     5' AND  PCO2 =',F6.1//,
     6' MEAN O2 DIFFUSING CAPACITY, ML/MIN/TORR  =',F8.3/)
      IF(ILT.EQ.1.AND.IPR.EQ.1)
     1WRITE(8,400)PO2,PCO2,DIFFPO,DIFFPC,PBARO2,PBARCO,DO2BAR
 400  FORMAT(//' ENDCAPILLARY PO2   =',F8.1,3X,
     1'  AND  PCO2 =',F6.1,//,
     2' ENDCAPILLARY-TISSUE PO2 DIFF =',F6.2,1X,
     3' & TISSUE-ENDCAPILLARY PCO2 DIFF =',F6.2,//,
     4' MEAN CAPILLARY PO2 =',F8.1,4X,
     5' AND  PCO2 =',F6.1//,
     6' MEAN O2 DIFFUSING CAPACITY, ML/MIN/TORR  =',F8.3/)
      IF(IPR.EQ.1)
     1WRITE(8,410)VO2,VCO2,R,O2DEL,O2EXT
 410  FORMAT(' VO2 =',F10.1,5X,'VCO2 =',F10.1,5X,
     1' AND R =',F5.2,//,
     2' O2 DELIVERY =',F10.1,'   AND % EXTRACTION =',F6.1/)
      RETURN
      END

